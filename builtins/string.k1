// TODO: CString type
deftype alias CString = { ptr: Pointer }
namespace CString { 
}

namespace string {

  fn get(self: string, index: u64): char {
    self.buffer.get(index)
  }

  fn fromBytes(buffer: Buffer[byte]): string {
    let copied: Buffer[byte] = buffer.cloned();
    let sneakyCasted: Buffer[char] = Buffer::fromRawParts(copied.len, copied.data);
    { buffer: sneakyCasted }
  }

  fn fromBuffer(buffer: Buffer[char]): string {
    let copied = buffer.cloned();
    { buffer: copied }
  }

  fn wrapList(list: List[char]): string {
    { buffer: list.toBuffer() }
  }

  fn wrapBuffer(buffer: Buffer[char]): string {
    { buffer: buffer }
  }

  fn len(self: string): u64 { self.buffer.len }

  fn isEmpty(self: string): bool { self.len() == 0 }
  fn nonEmpty(self: string): bool { self.len() != 0 }

  fn first(self: string): char? {
    if self.nonEmpty() .Some(self.get(0)) else .None
  }


  fn indexOf(self: string, c: char): i64 {
    let mut i: u64 = 0;
    while i < self.len() {
      if self.get(i) == c {
        return(i as i64);
      };
      i = i + 1;
    };
    -1
  }

  fn concat(self: string, other: string): string {
    fromBuffer(self.buffer.concat(other.buffer))
  }

  fn appendChar(self: string, c: char): string {
    let newLength = self.len() + 1;
    let newBuffer = self.buffer._enlargedClone(newLength);
    newBuffer.set(self.len(), c);
    fromBuffer(newBuffer)
  }

  fn splitByChar(self: string, delim: char): List[string] {
    let* results = List::empty[List[char]]();
    let* accum = List::empty[char]();
    // Note(stdlib slow): TODO: use ranges instead to make this zero-copy!!
    //                    We can point to the old string instead of building up new ones
    for c in self do {
      if c == delim {
        if accum.*.nonEmpty() {
          results.push(accum);
          accum <- List::empty[char]()
        }
      } else {
        accum.push(c);
      };
    };
    if accum.*.nonEmpty() {
      results.push(accum);
    };

    for list in results.* yield {
      string::wrapList(list)
    }
  }

  fn indexOfString(self: string, target: string): u64? {
    let mut i: u64 = 0;
    while i < self.len() {
      if i + target.len() >= self.len() {
        return(.None)
      };
      let sub = self.substring(i, i + target.len());
      if sub == target { return(.Some(i)) };
      i = i + 1;
    };
    .None
  }

  fn substring(self: string, start: u64, end: u64): string {
    { buffer: self.buffer.slice(start, end) }
  }

  fn startsWith(self: string, other: string): bool {
    self.substring(0, other.len()) == other
  }

  fn toCString(self: string): Pointer {
    self.appendChar('\0').buffer.data
  }

  fn toInt(self: string): int? {
    if self.isEmpty() { return(.None) };

    let neg = self.get(0) == '-';
    let mut i: u64 = if neg 1 else 0;
    let mut total: u64 = 0;
    while i < self.len() {
      let c = self.get(i);
      let digit = c.toAsciiDigit();
      if digit is .Some(digit) {
        total = (total * 10) + digit as u64;
      } else {
        return(.None);
      };
      i = i + 1;
    };

    some(
      if neg { (total as i64) * -1 } else { total as i64 }
    )
  }

}

impl Equals for string {
  fn equals(self: string, that: string): bool {
    self.buffer == that.buffer
  }
}

impl Equals for List[string] {
  fn equals(self: List[string], other: List[string]): bool {
    if self.len != other.len {
      false
    } else {
      let mut i: u64 = 0;
      while i < self.len {
        if self.get(i) != other.get(i) {
          return(false);
        };
        i = i + 1;
      };
      true
    }
  }
}

// One day? impl Show for List[T] ???
// Note(stdlib slow): Need efficient string building
impl Show for List[string] {
  fn show(self: List[string]): string {
    let b = StringBuilder::new();
    for s in self do {
      b.putString(s);
      let last = it_index == self.len - 1;
      if not last {
        b.putString(", ");
      };
    };
    b.putChar(']');
    b.build()
  }
}

impl Show for string {
  fn show(self: string): string { self }
}

impl Equals for Opt[string] {
  fn equals(self: Opt[string], other: Opt[string]): bool {
    opt_equals(self, other)
  }
}
