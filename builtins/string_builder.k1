deftype StringBuilder = { parts: Array[either Char(char), String(string)]* }
namespace StringBuilder {
  deftype alias Part = StringBuilder.parts.value.T

  fn withCapacity(cap: u64): StringBuilder {
    let parts = _root::new(Array::withCapacity[Part](cap));
    { parts: parts }
  }

  fn new(): StringBuilder {
    withCapacity(0)
  }

  fn putChar(self: StringBuilder, c: char): unit {
    self.parts.push(.Char(c))
  }
  fn putString(self: StringBuilder, s: string): unit {
    self.parts.push(.String(s))
  }
  fn putByte(self: StringBuilder, b: byte): unit {
    self.parts.push(.Char(b as char))
  }
  fn len(self: StringBuilder): u64 {
    let len: u64* = 0.&;
    for part in self.parts.* do {
      let partLen = switch part { .Char(_) -> 1: u64, .String(s) -> s.len };
      referenceSet(len, partLen);
    };
    len.*
  }
  fn build(self: StringBuilder): string {
    let bytes = Array::withCapacity[char](self.len()).&;
    for part in self.parts.* do {
      switch part {
        .Char(c) -> bytes.push(c),
        .String(s) -> {
          for c in s do {
            bytes.push(c)
          }
        }
      }
    };
    string::fromChars(bytes)
  }
}
