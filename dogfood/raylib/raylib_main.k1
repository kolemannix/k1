let MODULE_INFO: core/k1/ModuleManifest = {
  kind: .Executable,
  deps: [],
  multithreading: false,
  libs: ["raylib"]
};
ns raylib {
  deftype Color = {
    r: u8, g: u8, b: u8, a: u8
  }
  ns Color {
    fn rgba(r: u8, g: u8, b: u8, a: u8): u32 {
      let v: u32 = r.as();
      v := v | (g.as[u32]() << 8);
      v := v | (b.as[u32]() << 16);
      v := v | (a.as[u32]() << 24);
      v
    }
    fn u32(self: Color): u32 {
      rgba(self.r, self.g, self.b, self.a)
    }
  }

  deftype Rectangle = {
    x: f32, y: f32, width: f32, height: f32
  }

  extern("raylib","InitWindow") fn init(width: i32, height: i32, name: ptr)
  extern("raylib","CloseWindow") fn CloseWindow()
  extern("raylib","WindowShouldClose") fn WindowShouldClose(): bool

  extern("raylib","BeginDrawing") fn BeginDrawing()
  extern("raylib","EndDrawing") fn EndDrawing()
  extern("raylib","ClearBackground") fn ClearBackground(color: Color)

  // define void @DrawText(ptr noundef %0, i32 noundef %1, i32 noundef %2, i64 %3) #0 {
  extern("raylib","DrawText") fn DrawText(text: ptr, posX: i32, posY: i32, color: Color)

  extern("raylib","SetTargetFPS") fn SetTargetFPS(fps: i32)
  extern("raylib","GetFPS") fn GetFPS(): i32

}

let RED: raylib/Color = { r: 255, g: 0, b: 0, a: 255 };
let GRAY: raylib/Color = { r: 200, g: 200, b: 200, a: 255 };
let PINK: raylib/Color = { r: 255, g: 109, b: 194, a: 255 };
let PURPLE: raylib/Color = { r: 200, g: 122, b: 255, a: 255 };

#debug fn run(title: string): i32 {
  let context allocMode: mem/AllocMode = .Arena;
  let titleC = title.toCString();
  raylib/init(800, 450, titleC.ptr);
  raylib/SetTargetFPS(60);

  use raylib/ClearBackground;
  use raylib/DrawText;

  let i = 0;
  while not raylib/WindowShouldClose() {
    let fps = raylib/GetFPS();
    let color = if i < 100 PURPLE else PINK;
    ClearBackground(color);
    raylib/BeginDrawing();
    DrawText("Congrats! (fps: {fps})".toCString().ptr, 200, 200, GRAY);
    raylib/EndDrawing();
    i := i + 1;
    if i == 2000 { i := 0 };
  };

  raylib/CloseWindow();
  if k1/IS_STATIC 11 else 1
}

fn main(): i32 {
  //#static { 
  //  let ctResult = run("comptime!");
  //  println("Hey, the comptime result (printed at comptime btw) was {ctResult}");
  //  ctResult
  //};

  let rtResult = run("runtime!");
  println("Hey, the runtime result was {rtResult}");
  0
}

//#static {
//  run("comptime!")
//}
