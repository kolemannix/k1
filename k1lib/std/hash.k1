// Copyright (c) 2025 knix
// All rights reserved.

// This simple and unfinished hash table implementation is designed
// to exercise the language a bit. It exercises:
// - Generics
// - References/pointers
// - Structs and enums, optionals
// - Pattern matching
// - Loops and control flow
// - Numeric operations
// - String handling
// - Builds on List which is implemented in k1

// Demonstrates that the defaults of
// Library, no dependencies, not multithreaded work
let MODULE_INFO: core/k1/ModuleManifest = core/mem/zeroed();

deftype Entry[V] = ?{ key: string, value: V, deleted: bool }
deftype HashMap[V] = {
  size: size,
  // TODO: Switch to a 3-state enum for the deleted variant for a more efficient layout
  entries: List[Entry[V]],
}

fn fnv1a_hash(key: string): u64 {
  let FNV_PRIME: u64 = 1099511628211;
  let FNV_OFFSET: u64 = 14695981039346656037;
  let hash_value = FNV_OFFSET;
  for c in key do {
    hash_value := Bitwise/xor(hash_value, c as u8 as u64);
    hash_value := hash_value * FNV_PRIME;
  };
  hash_value
}

ns HashMap {

  fn _make[V](cap: size): HashMap[V] {
    { size: 0, entries: List/filled(cap, .None) }
  }

  fn new[V](): HashMap[V] {
    _make(0)
  }

  fn grow[V](self: *mut HashMap[V]): unit {
    let origSize = self.size;
    let oldEntries = self.entries;
    let newCap = if oldEntries.cap() == 0 1 else oldEntries.cap() * 2;

    let newMap = _make[V](newCap);
    self <- newMap;

    for v in oldEntries do {
      if v is .Some(v) {
        if not v.deleted self.insert(v.key, v.value)
      }
    };
    assertEquals(origSize, self.size)
  }

  fn _computeIndex[V](self: *HashMap[V], key: string): size {
    let hash = fnv1a_hash(key);
    let index = hash % (self.entries.len as u64);
    index as size
  }

  fn _probe[V](self: *mut HashMap[V], startIndex: size): *mut Entry[V] {
    let probeIndex = startIndex;
    loop {
      probeIndex := probeIndex + 1;
      if probeIndex == self.entries.len { probeIndex := 0 };

      let entryRef = self.entries*.getRef(probeIndex);
      if entryRef is .None* {
        return(entryRef)
      }
    }
  }

  fn fillFactor[V](self: HashMap[V]): f32 {
    let fillDivisor = if self.entries.len == 0 1 as f32 else self.entries.len as f32;
    let fillNumerator = if self.size == 0 1.0 else self.size as f32;
    let fillFactor = fillNumerator / fillDivisor;
    fillFactor
  }

  fn insert[V](self: *mut HashMap[V], key: string, value: V): unit {
    if self.*.fillFactor() > 0.7 {
      self.grow();
    };
    let index = self._computeIndex(key);
    let entryRef = self.entries*.getRef(index);
    let entry = { key: key, value: value, deleted: false };
    switch entryRef.* {
      .None -> {
        entryRef <- .Some(entry);
        self.size* <- self.size + 1;
      },
      .Some({ key: existingKey, value: _ }) -> {
        if existingKey == key {
          entryRef <- .Some(entry);
        } else {
          let nextAvailableEntry = self._probe(index);
          nextAvailableEntry <- .Some(entry);
          self.size* <- self.size + 1;
        }
      }
    };
  }

  fn get[V](self: *HashMap[V], key: string): ?V {
    let index = self._computeIndex(key);
    let slot = self.entries.get(index);
    require slot is .Some({ key: existingKey, value: v, deleted }) else {
      return(.None)
    };
    if existingKey == key {
      if deleted .None else .Some(v)
    } else {
      let probeIndex = index;
      loop {
        probeIndex := probeIndex + 1;
        if probeIndex == self.entries.len { probeIndex := 0 };

        switch self.entries.get(probeIndex) {
          .Some({ key: existingKey, value, deleted }) -> {
            if key == existingKey {
              if deleted break(.None)
              else break(.Some(value))
            }
          },
          .None -> { break(.None) },
        }
      }
    }
  }

  fn remove[V](self: *mut HashMap[V], key: string): bool {
    let index = self._computeIndex(key);
    let entryRef = self.entries*.getRef(index);
    require entryRef is .Some({ key: existingKey, value: value, deleted })* else {
      return(false)
    };
    if key == existingKey {
      if deleted.* {
        (false)
      } else {
        deleted <- true;
        self.size* <- self.size - 1;
        true
      }
    } else {
      let probeIndex = index;
      loop {
        probeIndex := probeIndex + 1;
        if probeIndex == self.entries.len { probeIndex := 0; };

        let probedEntryRef = self.entries*.getRef(probeIndex);
        switch probedEntryRef {
          .Some({ key: existingKey, value, deleted })* -> {
            if key == existingKey.* {
              let wasDeleted = deleted.*;
              if not wasDeleted {
                self.size* <- self.size - 1;
                deleted <- true;
                break(true)
              } else {
                break(false)
              }
            }
          },
          .None* -> { break(false) },
        }
      }
    }
  }

  fn dump[V](self: HashMap[V]): unit where V: Print {
    let capacity = self.entries.len;
    let size = self.size;
    println("Size: {size}, Capacity: {capacity}");
    for self.entries do {
      print("  SLOT {itIndex}: ");
      switch it {
        .None -> println("empty"),
        .Some({ key, value, deleted }) -> {
          println("{key} -> {value} (d={deleted})");
        }
      }
    }
  }

}
