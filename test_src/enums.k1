fn enumNoPayload(): unit {
  let mut v: either Yes, No = .No;
  if v is .No { () } else { crash("") };
  v = .Yes;
  if v is .Yes { () } else { crash("") };
}

deftype Maybe = either A, B(char), C(string)
fn enumPayload(): unit {
  let a: Maybe = .A;
  let b: Maybe = .B('x');
  let c: Maybe = .C("hello, enums");
  if a is .A {
    ()
  } else crash("");
  assert(a is .A);
  assert(a.asA().hasValue());

  if b is .B(x) {
    assert(x == 'x');
  } else crash("");
  assert(b is .B(_));
  assert(b.asB()!.value == 'x');

  if c is .C("hello, enums") {
    ()
  } else crash("");
  assert(c is .C(_));
  assert(c.asC()!.value == "hello, enums");

}

deftype Result[T, E] = either Ok(T), Err(E)
namespace Result {
  fn unwrap[T, E](self: Result[T, E]): T {
    self.asOk()!.value
  }
  fn show(self: Result[int, string]): string {
    switch self {
      .Ok(v) -> { "Ok: \{v}\n" },
      .Err(e) -> { "Error: \{e}\n" }
    }
  }
}

fn genericEnum(): unit {
    let x1: Result[int, string] = .Ok(-3);
    let x2: Result[int, string] = .Err("Some error message");

    let x1Ok = x1.asOk()!;

    // This tests type inference for enums
    assert(Result::unwrap(x1) == -3);
    assert(x1.unwrap() == -3);
    assert(x1Ok.unwrap() == -3);

    // Shows that we can talk about the payload type
    let b: Result[int, string].Ok.value = 42;

    assert(x1.show() == "Ok: -3\n");
    assert(x2.show() == "Error: Some error message\n");
    printIt(sizeOf[Result[int, string].Ok]());
    printIt(alignOf[Result[int, string].Ok]());
    assert(sizeOf[Result[int, string].Ok]() == 24);
    assert(alignOf[Result[int, string].Ok]() == 8);
}

fn main(): int {
  enumNoPayload();
  enumPayload();
  genericEnum();
  0
}
