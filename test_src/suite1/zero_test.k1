ns zero_test;

use core/mem/zeroed

fn testBitcast(): unit {
  let x: i32 = -1;
  let y: u32 = mem/bitcast(x);
  assertEquals(y, 0xffff_ffff);

  // From agg to agg
  let s: string = "foo";
  let sWrap = { inner: s };
  let sAgain: string = mem/bitcast(sWrap);
  assertEquals(s, sAgain);

  // From agg to scalar
  let p2: { x: f32, y: f32 } = { x: 1.0, y: -1.0 };
  let scalar = mem/bitcast[_, i64](p2);
  let p3: { x: f32, y: f32 } = mem/bitcast(scalar);
  assertEquals(p2.x, p3.x);
  assertEquals(p2.y, p3.y);

  // From scalar to scalar
  let f: f32 = -3.14159;
  let i: i32 = mem/bitcast(f);
  let f2: f32 = mem/bitcast(i);
  assertEquals(f, f2);

  // From scalar to agg
  let ipoint: u32 = 0b0000_0000_0000_0001_0000_0000_0000_0010;
  let p: { x: i16, y: u16 } = mem/bitcast(ipoint);
  // Note: endianness-dependent result
  assertEquals(p.y, 1);
  assertEquals(p.x, 2);

  assert(testCompile(
    mem/bitcast[u8, u16](4)
  ).isSome());
}

fn testZero(): unit {
  let v8: u8 = zeroed();
  let v16: u16 = zeroed();
  let v32: u32 = zeroed();
  let v64: u64 = zeroed();
  assertEquals(v8, 0);
  assertEquals(v16, 0);
  assertEquals(v32, 0);
  assertEquals(v64, 0);

  let vi8: i8 = zeroed();
  let vi16: i16 = zeroed();
  let vi32: i32 = zeroed();
  let vi64: i64 = zeroed();
  assertEquals(vi8, 0);
  assertEquals(vi16, 0);
  assertEquals(vi32, 0);
  assertEquals(vi64, 0);

  let vword: i64 = zeroed();
  let vuword: u64 = zeroed();
  assertEquals(vword, 0);
  assertEquals(vuword, 0);

  let vf32: f32 = zeroed();
  assert(vf32 == 0.0);
  let vf64: f64 = zeroed();
  assert(vf64 == 0.0);

  let* i32Ptr: *i32 = zeroed();
  assertEquals(i32Ptr.*, 0);
  let i32Null: *i32 = zeroed();
  assert(i32Null.as[ptr] == ptr/NULL);

  let point: { x: u8, y: u8, z: i32 } = zeroed();
  assertEquals(point.x, 0);
  assertEquals(point.y, 0);
  assertEquals(point.z, 0);

  let o: ?typeOf(point) = zeroed();
  assertEquals(o.tag, 0);


  let point = #static {
    ({ x: 1, y: #static 2 })
  };
  #static "I should have enough alt vms for this";
  #static "I should have enough alt vms for this too";
  assertEquals(point.x, 1);

  ()
}
