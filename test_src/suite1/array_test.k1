ns array_test;

deftype Thing = {
  data: Array[u8, 1024],
  data2: Array[u64, 1024],
}

fn testLiterals(): unit {
  let* array: *mut Array[bool, 3] = [false, true, true];
  let buffer: Buffer[bool] = [false, true, true];
  let list: List[bool] = [false, true, true];
  assertEquals(array.asBuffer(), buffer);
  assertEquals(list.asBuffer(), buffer);
}

fn testFilled(): unit {
  let context alloc: mem/AllocMode = .Arena;
  let buffer: Buffer[u8] = Buffer/filledIn(size = 64, value = 3);
  let array: *Array[u8, 64] = Array/filledIn(3, 64);
  let list: List[u8] = List/filledIn(size = 64, value = 3);
  assertEquals(buffer.len, 64);
  assertEquals(array.len(), 64);
  assertEquals(list.len, 64);
  for buffer {
    assertEquals(buffer.get(itIndex), 3);
    assertEquals(list.get(itIndex), 3);
    assertEquals(array.get(itIndex), 3);
  }
}

fn testArray(): unit {
  let context alloc: mem/AllocMode = .Arena;
  let ls: List[u8] = List/filledIn(size = 10, value = 3);

  let a: *mut Array[u8, 10] = List/copyToArray(ls, 10);
  for core/IntRange/upTo(a.len()) {
    if it % 2 == 0 { a.getRef(it) <- 1 };
  };
  for core/IntRange/upTo(a.len()) {
    if it % 2 == 0 {
      assertEquals(a.get(it), 1);
    } else {
      assertEquals(a.get(it), 3);
    }
  };

}

fn test(): unit {
  assertEquals(types/sizeOf[Thing](), 9216);

  testArray();

  testLiterals();
  testFilled();

  let* array: *mut Array[f32, 10] = mem/zeroed();
  assert(testCompile(array.get(11)) is .Some(_));
  array.set(0, 3.14);
  let buf = Buffer/wrapArray(array);
  assert(buf.get(0) == array.get(0));
  assertEquals(buf.get(0), array.get(0));

  ()
}
