ns enum_basic;

fn enumNoPayload(): unit {
  let v: either Yes, No = .No;
  if v is .No { () } else { crash("") };
  v := .Yes;
  if v is .Yes { () } else { crash("") };
}

deftype Maybe = either A, B(char), C(string)
fn enumPayload(): unit {
  let a: Maybe = .A;
  let b: Maybe = .B('x');
  let c: Maybe = .C("hello, enums");
  if a is .A {
    ()
  } else crash("");
  assert(a is .A);

  if b is .B(x) {
    assert(x == 'x');
  } else crash("");
  assert(b is .B(_));
  assert(b is .B('x'));
  assert(b.asB().! == 'x');

  if c is .C("hello, enums") {
    ()
  } else crash("");
  assert(c is .C(_));
  assert(c.asC().! == "hello, enums");

}

deftype MyResult[T, E] = either Ok(T), Err(E)
ns MyResult {
  fn myGet[T, E](self: MyResult[T, E]): T {
    self.asOk().!
  }
  fn show[T: Print, E: Print](self: MyResult[T, E]): string {
    switch self {
      .Ok(v) -> { "Ok: {v}\n" },
      .Err(e) -> { "Error: {e}\n" }
    }
  }
}

fn genericEnum(): unit {
    let x1: MyResult[int, string] = .Ok(-3);
    let x2: MyResult[int, string] = .Err("Some error message");

    let x1Ok = x1.asOk();

    // This tests type inference for enums
    assert(MyResult/myGet(x1) == -3);
    assert(x1.myGet() == -3);
    assert(x1Ok == .Some(-3));

    // Shows that we can talk about the payload type
    let b: MyResult[int, string].Ok = 42;

    assertEquals(x1.show(), "Ok: -3\n");
    assertEquals(x2.show(), "Error: Some error message\n");

    assert(core/types/sizeOf[MyResult[int, string]]() == 24);
    assert(core/types/sizeOfStride[MyResult[int, string]]() == 24);
    assert(core/types/alignOf[MyResult[int, string]]() == 8);
}

// Bug: change this type to name 'E' for a nice surprise
deftype EByte = either(u8) A, B, C
fn tags(): unit {
  let a: EByte = .A;
  let b: EByte = .B;
  let c: EByte = .C;
  assert(a.tag == 0);
  assert(b.tag == 1);
  assert(c.tag == 2);
  ()
}

fn test(): unit {
  tags();
  enumNoPayload();
  enumPayload();
  genericEnum();
}
