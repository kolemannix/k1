ns lambdas {

fn staticAddOne(x: int): int { x + 1 }
fn staticAddTwo(x: int): int { x + 2 }

fn captures(): unit {
  let closedOver: int = 3;

  let add = \(x: int) {
    let addOne = \(y: int) y + 1;
    addOne(x) + closedOver + addOne(closedOver) + (if closedOver == 3 1 else 0);
  };

  assertEquals(add(3), 12);
}

fn captureMut(): unit {
  let* mut counter = 3;

  let inc = \(x: int) {
    counter <- counter.* + 1;
  };

  inc(0);
  inc(0);
  inc(0);

  assert(counter.* == 6);
}

fn closureReturn(): unit {
  let getThree = \(-> int) { return(3) };
  let nullary = \() 4;
  assert(getThree() == 3);
  assert(nullary() == 4);
}

fn simpleDyn(thunk: dyn[\()-> unit]): unit {
  thunk()
}

fn someFnParam1(i: int, f: some \int -> int): int {
  f(i)
}

fn someFnParam2(i: int, f: some \int -> int, g: some \int -> int): int {
  f(i) + g(i)
}

fn someFnParamGen[T](t: T, f: some \T -> T, g: some \T -> ?T): ?T {
  t | f | g
}

fn runThings(thunks: List[dyn[\()-> unit]]): unit {
  for thunk in thunks do { thunk() }
}

fn thunksTest(): unit {
  let* mut counter = 0;
  let thunks: List[dyn[\() -> unit]] = [
    \() { counter <- counter.* + 1 },
    \() { counter <- counter.* + 1 },
    \() { counter <- counter.* + 1 },
  ];

  runThings(thunks);
  println("counter is {counter.*}");
}

fn test(): unit {

  assert(staticAddOne.toRef() as Pointer == staticAddOne.toRef() as Pointer);

  assert(staticAddOne.toRef() as Pointer != staticAddTwo.toRef() as Pointer);

  let staticAddOneVar = staticAddOne.toRef();
  let staticResult = someFnParam2(5, staticAddOne.toRef(), staticAddOneVar);
  assertEquals(staticResult, 6 + 6);

  let lam1ResultPtr = someFnParam1(5, staticAddOne.toRef());
  assertEquals(lam1ResultPtr, 6);

  let lam1ResultDyn = someFnParam1(5, staticAddOne.toDyn());
  assertEquals(lam1ResultDyn, 6);

  let* c1 = { x: 3 };
  let c2 = 2;

  let withClosures = someFnParam2(5, \x. x + 1 + c1.x, \x. x + 2 + c2);
  assertEquals(withClosures, (5 + 1 + 3) + (5 + 2 + 2));

  let withGenerics = someFnParamGen(true, \b. not b, \b. some(b));
  assert(withGenerics == some(false));

  use List/filter; use List/map;
  let result = [1,2,3,5,6,7,8]
    | map(\x. x + 1)
    | filter(\x. x % 2 == 0);
  assert(result == [2, 4, 6, 8]);

  let x = 10;
  // Coerce lambdas to lambda objects
  simpleDyn(\() println("Thunk! {x}"));
  // Coerce function refs to lambda objects
  simpleDyn(\() println("Thunk!"));

  thunksTest();

  captures();
  captureMut();
  closureReturn();
  ()
}

}
