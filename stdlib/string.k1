// TODO: CString type
deftype alias CString = Pointer
ns CString { 
}

ns string {

  fn get(self: string, index: uword): char {
    self.buffer.get(index)
  }

  fn innerBuffer(self: string): Buffer[char] { self.buffer }

  fn innerBufferBytes(self: string): Buffer[byte] {
    self.innerBuffer().toByteBuffer()
  }

  fn clone(self: string): string {
    let newBuffer = self.buffer.cloned();
    wrapBuffer(newBuffer)
  }

  fn cloneToList(self: string): List[char] {
    List/wrapBuffer(self.clone().buffer)
  }

  fn wrapList(list: List[char]): string {
    { buffer: list.toBuffer() }
  }

  fn wrapBuffer(buffer: Buffer[char]): string {
    { buffer: buffer }
  }

  fn wrapBytes(buffer: Buffer[byte]): string {
    { buffer: buffer.toCharBuffer() }
  }

  fn fromBytes(buffer: Buffer[byte]): string {
    let copied: Buffer[byte] = buffer.cloned();
    wrapBytes(copied)
  }

  fn fromBuffer(buffer: Buffer[char]): string {
    let copied = buffer.cloned();
    { buffer: copied }
  }

  fn len(self: string): uword { self.buffer.len }

  fn isEmpty(self: string): bool { self.len() == 0 }
  fn nonEmpty(self: string): bool { self.len() != 0 }

  fn first(self: string): char? {
    if self.nonEmpty() .Some(self.get(0)) else .None
  }

  fn last(self: string): char? {
    if self.nonEmpty() .Some(self.get(self.len() - 1)) else .None
  }


  fn indexOf(self: string, c: char): i64 {
    let mut i: uword = 0;
    while i < self.len() {
      if self.get(i) == c {
        return(i as i64);
      };
      i = i + 1;
    };
    -1
  }

  fn concat(self: string, other: string): string {
    fromBuffer(self.buffer.concat(other.buffer))
  }

  fn appendChar(self: string, c: char): string {
    let newLength = self.len() + 1;
    let newBuffer = self.buffer._enlargedClone(newLength);
    newBuffer.set(self.len(), c);
    fromBuffer(newBuffer)
  }

  fn splitByChar(self: string, delim: char): List[string] {
    let* results = List/empty[List[char]]();
    let* accum = List/empty[char]();
    // Note(stdlib slow): TODO: use ranges instead to make this zero-copy!!
    //                    We can point to the old string instead of building up new ones
    for c in self do {
      if c == delim {
        if accum.*.nonEmpty() {
          results.push(accum);
          accum <- List/empty[char]()
        }
      } else {
        accum.push(c);
      };
    };
    if accum.*.nonEmpty() {
      results.push(accum);
    };

    for list in results.* yield {
      string/wrapList(list)
    }
  }

  fn indexOfString(self: string, target: string): uword? {
    let mut i: uword = 0;
    while i < self.len() {
      if i + target.len() >= self.len() {
        return(.None)
      };
      let sub = self.substring(i, i + target.len());
      if sub == target { return(.Some(i)) };
      i = i + 1;
    };
    .None
  }

  fn substring(self: string, start: uword, end: uword): string {
    { buffer: self.buffer.slice(start, end) }
  }

  fn take(self: string, count: uword): string { self.substring(0,     count) }
  fn drop(self: string, count: uword): string { self.substring(count, self.len()) }

  fn startsWith(self: string, other: string): bool {
    self.substring(0, other.len()) == other
  }

  fn dataPointer(self: string): Pointer {
    self.buffer.dataPointer()
  }

  fn toCString(context alloc: mem/AllocMode)(self: string): { ptr: Pointer, len: uword } {
    if self.isEmpty()                  { ptr: "\0".dataPointer(), len: 0 }
    else if self.last() is .Some('\0') { ptr: self.dataPointer(), len: self.len() - 1 }
    else                               { ptr: self.appendChar('\0').dataPointer(), len: self.len() }
  }

  fn fromCString(cString: Pointer): string {
    let* self = List/empty[char]();
    let mut i = 0: uword;
    loop {
      let c = cString.refAtIndex[char](i);
      if c.* == '\0' {
        break();
      } else {
        self.push(c);
        i = i + 1;
      }
    };
    wrapList(self)
  }

  fn toInt(self: string): int? {
    if self.isEmpty() { return(.None) };

    let neg = self.get(0) == '-';
    let mut i: uword = if neg 1 else 0;
    let mut total: uword = 0;
    while i < self.len() {
      let c = self.get(i);
      let digit = c.toDigitValue();
      if digit is .Some(digit) {
        total = (total * 10) + digit as uword;
      } else {
        return(.None);
      };
      i = i + 1;
    };

    some(
      if neg { (total as int) * -1 } else { total as int }
    )
  }

}

impl Equals for string {
  fn equals(self: string, that: string): bool {
    self.buffer == that.buffer
  }
}

impl Show for string {
  fn show(self: string): string { self }
}

impl Iterable[Item = char, I = BufferIterator[char]] for string {
  fn iterator(self: string): I { BufferIterator/fromBuffer(self.buffer) }
}

impl Iterable[Item = char, I = BufferIterator[char]] for string* {
  fn iterator(self: string*): I { self.*.iterator() }
}
